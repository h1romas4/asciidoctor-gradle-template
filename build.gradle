plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
}

repositories {
    mavenCentral()
    ruby.gems()
}

dependencies {
    asciidoctorGems files('gradle/repos/gem/prawn_svg_font_patch-0.1.0.gem')
    // only asciidoctor-pdf 1.5.3 - 1.6.2
    asciidoctorGems files('gradle/repos/gem/asciidoctor-nabetani-0.1.4-ruby25-patched.gem')
    // for another asciidoctor-pdf version
    // asciidoctorGems 'rubygems:asciidoctor-pdf-linewrap-ja:0.7.1'
}

asciidoctor {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    sources {
        include 'index.adoc'
    }
    resources {
        from('src/docs/asciidoc') {
            include 'Chapter*/images/*'
        }
    }
    asciidoctorj {
        attributes 'stylesdir': '@style',
            'stylesheet': 'asciidoctor.css'
    }
}

asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    fontsDir file('src/docs/asciidoc/@font')
    sources {
        include 'index.adoc'
    }
    asciidoctorj {
        attributes 'pdf-themesdir': "@style",
            'pdf-theme': 'pdf-theme.yml'
            // When using the asciidoctor-pdf standard ward-wrap
            //, 'scripts': 'cjk'
    }
}

asciidoctorj {
    version = '2.5.6'
    modules {
        diagram.use()
        diagram.version '2.2.8'
        // If asciidoctor-nabetani is used,
        // asciidoctor-pdf must be specified as 1.5.3 - 1.6.2.
        pdf.version '1.6.2'
    }
    requires = [
        'asciidoctor-diagram',
        'prawn_svg_font_patch',
        // only asciidoctor-pdf 1.5.3 - 1.6.2
        'asciidoctor/nabetani',
        // for another asciidoctor-pdf version
        // 'asciidoctor-pdf-linewrap-ja',
    ]
    attributes 'source-highlighter': 'rouge'
}

/**
 * docs/ 配下のファイルを全て削除するタスク
 * docs タスクの依存で直接は実行しない。
 */
task cleanDocs(type: Delete) {
    delete fileTree('docs/') {
        include '**/*'
    }
}

/**
 * Asciidoc 文章とファイルシステムの画像の整合性を確認するユーティリティタスク
 * 未使用画像とリンク切れ画像の一覧を出力する。
 *
 * ./gradlew syncImage
 *
 * @see buildSrc/src/main/groovy/AsciidocTasks.groovy
 */
tasks.register('syncImage', SyncImageTask) {
    // 起点の文書ディレクトリ
    baseDir = file('src/docs/asciidoc')
    // 起点の Asciidoc 文書（include を辿る）
    index = 'index.adoc'
    // ファイルシステム上で画像として認識する拡張子
    imageExt = ['png', 'jpg', 'jpeg', 'svg']
}

/**
 * Asciidoc 文書から HTML/PDF 文書を生成し docs 配下に出力するタスク
 *
 * ./gradlew docs
 */
task docs(dependsOn: [asciidoctor, asciidoctorPdf, cleanDocs]) doLast {
    // build に生成された文書を docs にコピー
    copy {
        from 'build/docs/asciidoc/index.html'
        from 'build/docs/asciidocPdf/index.pdf'
        into 'docs/'
    }
    // build に出力されたダイアログ生成画像を含む全ての画像を HTML 文書用に docs にコピー
    copy {
        from 'build/docs/asciidoc/'
        include 'Chapter*/images/*'
        into 'docs'
    }
}
