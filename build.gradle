plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.pdf' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.0'
}

repositories {
    mavenCentral()
    ruby.gems()
}

dependencies {
    asciidoctorGems files('gradle/repos/gem/prawn_svg_font_patch-0.1.0.gem')
    // only asciidoctor-pdf 1.5.3
    asciidoctorGems files('gradle/repos/gem/asciidoctor-nabetani-0.1.4-ruby25-patched.gem')
    // for another asciidoctor-pdf version
    // asciidoctorGems 'rubygems:asciidoctor-pdf-linewrap-ja:0.7.1'
}

asciidoctor {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    sources {
        include 'index.adoc'
    }
    resources {
        from('src/docs/asciidoc') {
            include 'Chapter*/images/*'
        }
    }
    asciidoctorj {
        attributes 'stylesdir': '@style',
            'stylesheet': 'asciidoctor.css'
    }
}

asciidoctorPdf {
    dependsOn asciidoctorGemsPrepare
    baseDir file('src/docs/asciidoc')
    fontsDir file('src/docs/asciidoc/@font')
    sources {
        include 'index.adoc'
    }
    asciidoctorj {
        attributes 'pdf-stylesdir': "@style",
            'pdf-style': 'pdf'
    }
}

asciidoctorj {
    modules {
        diagram.use()
        diagram.version '2.0.5'
        pdf.version '1.5.3'
    }
    requires = [
        'asciidoctor-diagram',
        'prawn_svg_font_patch',
        // only asciidoctor-pdf 1.5.3
        'asciidoctor/nabetani',
        // for another asciidoctor-pdf version
        // 'asciidoctor-pdf-linewrap-ja',
    ]
    attributes 'source-highlighter': 'coderay'
}

task cleanDocs(type: Delete) {
    delete fileTree('docs/') {
        include '**/*'
    }
}

task docs(dependsOn: [asciidoctor, asciidoctorPdf, cleanDocs]) doLast {
    copy {
        from 'build/docs/asciidoc/index.html'
        from 'build/docs/asciidocPdf/index.pdf'
        into 'docs/'
    }
    copy {
        from 'src/docs/asciidoc/'
        include 'Chapter*/images/*'
        into 'docs'
    }
}
